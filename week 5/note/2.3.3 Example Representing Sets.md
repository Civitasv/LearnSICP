# 2.3.3 Example: Representing Sets

## Sets as unordered lists

```Scheme
(define (element-of-set? x set)
  (cond ((null? set) #f)
        ((equal? x (car set)) #t)
        (else (element-of-set? x (cdr set)))))

(define (adjoin-set x set)
  (if (element-of-set? x set)
      set
      (cons x set)))

(define (intersection-set set1 set2)
  (cond ((or (null? set1) (null? set2)) '())
        ((element-of-set? (car set1) set2)
         (cons (car set1) (intersection-set (cdr set1) set2)))
        (else (intersection (cdr set1) set2))))
```

- `element-of-set?`: $\theta(n)$
- `adjoin-set`: $\theta(n)$
- `intersection-set`: $\theta(n^2)$

## Sets as ordered lists

One way to speed up our set operations is to change the representation so that the set elements are listed in increasing order.

To keep our discussion simple, we will consider only the case where the set elements are numbers, so that we can compare elements using `>` and `<`.

```Scheme
(define (element-of-set? x set)
  (cond ((null? set) false)
        ((= x (car set)) true)
        ((< x (car set)) false)
        (else (element-of-set? x (cdr set)))))
```

The average number of steps required will be about `n/2`.

We obtain a more impressive speedup with `intersection-set`.

```Scheme
(define (intersection-set set1 set2)
  (if (or (null? set1) (null? set2))
      '()
      (let ((x1 (car set1))
            (x2 (car set2)))
        (cond ((= x1 x2) (cons x1 (intersection-set (cdr set1) (cdr set2))))
              ((< x1 x2) (intersection-set (cdr set1) set2))
              ((> x1 x2) (intersection-set set1 (cdr set2)))))))
```

Thus, the number of steps required is at most the sum of the sizes of `set1` and `set2`. This is $\theta(n)$ growth rather than $\theta(n^2)$.

## Sets as binary trees

We can do better than the ordered-list representation by arranging the set elements in the form of a tree.

Each node of the tree holds one element of the set, called the "entry" at that node, and a link to each of two other nodes. The "left" link points to elements smaller than the one at the node, and the "right" link to elements greater than the one at the node.

The advantage of the tree representation is this: Suppose we want to check whether a number x is contained in a set. We begin by comparing x with the entry in the top node. If x is less than this, we know that we need only search the left subtree; if x is greater, we need only search the right subtree. Now, if the tree is “balanced,” each of these subtrees will be about half the size of the original. Thus, in one step we have reduced the problem of searching a tree of size _n_ to searching a tree of size _n/2_. Since the size of the tree is halved at each step, we should expect that the number of steps needed to search a tree of size _n_ grows as $\theta(log n)$.

```Scheme
(define (entry tree) (car tree))
(define (left-branch tree) (cadr tree))
(define (right-branch tree) (caddr tree))
(define (make-tree entry left right) (list entry left right))

(define (element-of-set? x set)
  (cond ((null? set) false)
        ((= x (entry set)) true)
        ((> x (entry set)) (element-of-set? x (right-branch set)))
        ((< x (entry set)) (element-of-set? x (left-branch set)))))

(define (adjoin-set x set)
  (cond ((null? set) (make-tree x '() '()))
        ((= x (entry set)) set)
        ((< x (entry set)) (make-tree (entry set) (adjoin-set x (left-branch set)) (right-branch set)))
        ((> x (entry set)) (make-tree (entry set) (left-branch set) (adjoin-set x (right-branch set))))))
```

## Sets and information retrieval
